// Открывает форму списка выбора сотрудника, должности, оклада
//
// Параметры:
//  Данные	 -  ТекущиеДанные	- строка с данными для отбора 
//  Элемент	 - 	ЭлементФормы	- элемент, в событие ОбработкаВыбора которого вернется структура выбора
//  Отбор	 - 	Структура		- структура с ключами - именами полей данных для отбора
//                                Сотрудник, Должность, Дата(Период)
// В событие ОбработкаВыбора в ВыбранноеЗначение передаётся структура с полями
// 	Сотрудник		- Справочник.ФизическиеЛица	- Выбранный сотрудник
// 	Должность		- Справочник.Должности		- Должность выбранного сотрудника на указанную дату
// 	Оклад           - Деньги					- Величина оклада на указанную дату
// 	ВидНазначения	- Перечисление.ВидЗанимаемойДолжности	- Основная или Совмещение
// 	ЭтоТекущая		- Булево					- это основная должность сейчас
&НаКлиенте
Процедура ОткрытьФормуВыбораСотрудника(Данные, Элемент, Отбор, ПараметрыОткрытия = Неопределено) Экспорт
    Если ПараметрыОткрытия = Неопределено Тогда
		ПараметрыОткрытия = Новый Структура();
	КонецЕсли; 
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);

	ЕстьСотрудник = ЗаполнитьСтруктуруОтбораВыбора(Данные, Отбор, "Сотрудник");
	ЕстьДолжность = ЗаполнитьСтруктуруОтбораВыбора(Данные, Отбор, "Должность");
	Если ЕстьСотрудник Тогда
		Строка = "" + Отбор.Сотрудник;
		Если ЕстьДолжность Тогда
			Строка = Строка + " (" + Отбор.Должность + ")";
		КонецЕсли; 
		ПараметрыОткрытия.Вставить("ТекущаяСтрока", Строка);
	КонецЕсли;
	
	Если Данные.Свойство("Дата") Тогда
		ЗаполнитьСтруктуруОтбораВыбора(Данные, Отбор, "Период", "Дата");
	Иначе
		ЗаполнитьСтруктуруОтбораВыбора(Данные, Отбор, "Период");
	КонецЕсли; 

	Если Отбор.Количество() <> 0 Тогда
		ПараметрыОткрытия.Вставить("Отбор", Отбор);
	КонецЕсли;
	ОткрытьФорму("Справочник.ФизическиеЛица.Форма.ФормаСпискаПоДолжности"
		, ПараметрыОткрытия, Элемент
		, , , ,
		, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьСтруктуруОтбораВыбора(Данные, Отбор, КлючОтбора, КлючДанных = Неопределено)
	Если КлючДанных = Неопределено Тогда
		КлючДанных = КлючОтбора;
	КонецЕсли;
	Результат = Отбор.Свойство(КлючОтбора); 
	Если Результат И Не ЗначениеЗаполнено(Отбор[КлючОтбора]) Тогда
		Если Данные.Свойство(КлючДанных) И ЗначениеЗаполнено(Данные[КлючДанных]) Тогда
			Отбор[КлючОтбора] = Данные[КлючДанных];
		Иначе
			Отбор.Удалить(КлючОтбора);
			Результат = Ложь;                                                   
		КонецЕсли;
	КонецЕсли;
	Возврат Результат; 
КонецФункции
